Tame Metal Porcupine

Medium

# The `CNumaToken.leverageStrategy()` function reverts due to an improper check of the `principal` borrow amount.

### Summary

In the `leverageStrategy()` function, after the `borrowInternalNoTransfer()` call, there is a check for the `principal` borrow amount. This check requires that the difference between the principal borrow amounts before and after the call to `borrowInternalNoTransfer()` matches the new borrow amount. However, this requirement cannot be satisfied, resulting in the transaction reverting.

### Root Cause

As you can see at [line 201](https://github.com/sherlock-audit/2024-12-numa-audit/blob/main/Numa/contracts/lending/CNumaToken.sol#L201), the `leverageStrategy()` function requires that the difference between the principal borrow amounts, before and after the call to the `borrowInternalNoTransfer()` function, matches the new borrow amount.

However, this requirement cannot be satisfied.

```solidity
    function leverageStrategy(
        uint _suppliedAmount,
        uint _borrowAmount,
        CNumaToken _collateral,
        uint _strategyIndex
    ) external {
        // AUDITV2FIX if we don't do that, borrow balance might change when calling borrowinternal
        accrueInterest();
        _collateral.accrueInterest();

        ...

196     uint accountBorrowBefore = accountBorrows[msg.sender].principal;
        
        borrowInternalNoTransfer(borrowAmount, msg.sender);
        
        require(
201         (accountBorrows[msg.sender].principal - accountBorrowBefore) ==
                borrowAmount,
            "borrow ko"
        );

        ...
    }
```

Let's examine the `borrowInternalNoTransfer()` function.

As you can see at line 782, the principal amount becomes `accountBorrowsPrev + borrowAmount`. Here, `accountBorrowsPrev` is not the original principal amount but the principal amount adjusted by the new `borrowIndex` within the `borrowBalanceStoredInternal()` function.

The `borrowIndex` is modified during the `accrueInterest()` function, which is called at the beginning of the `leverageStrategy()` function.

Thus, the principal borrow amount is first adjusted and then incremented by the new borrow amount.

As a result, the actual difference between the principal borrow amounts slightly differs from the new borrow amount.

```solidity
    function borrowFreshNoTransfer(
        address payable borrower,
        uint borrowAmount
    ) internal virtual {
        ...

770     uint accountBorrowsPrev = borrowBalanceStoredInternal(borrower);
771     uint accountBorrowsNew = accountBorrowsPrev + borrowAmount;
        
        ...
        
782     accountBorrows[borrower].principal = accountBorrowsNew;
        
        ...
    }

--------------------

    function borrowBalanceStoredInternal(
        address account
    ) internal view returns (uint) {
        ...

352     uint principalTimesIndex = borrowSnapshot.principal * borrowIndex;
353     return principalTimesIndex / borrowSnapshot.interestIndex;
    }
```

### Internal pre-conditions

### External pre-conditions

### Attack Path

### Impact

The `leverageStrategy()` function is useless, undermining the core functionality of the protocol.

### PoC

### Mitigation

Use `borrowBalanceStoredInternal` instead of `principal` for `accountBorrowBefore`.

```diff
    function leverageStrategy(
        uint _suppliedAmount,
        uint _borrowAmount,
        CNumaToken _collateral,
        uint _strategyIndex
    ) external {
        // AUDITV2FIX if we don't do that, borrow balance might change when calling borrowinternal
        accrueInterest();
        _collateral.accrueInterest();

        ...

-       uint accountBorrowBefore = accountBorrows[msg.sender].principal;
+       uint accountBorrowBefore = borrowBalanceStoredInternal(msg.sender);
        
        borrowInternalNoTransfer(borrowAmount, msg.sender);
        
        require(
            (accountBorrows[msg.sender].principal - accountBorrowBefore) ==
                borrowAmount,
            "borrow ko"
        );

        ...
    }
```