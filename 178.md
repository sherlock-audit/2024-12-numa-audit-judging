Bright Lipstick Turkey

Medium

# Incorrect price for negative ticks due to lack of rounding down in `NumaOracle::getV3SqrtPriceAvg`

### Summary

The `NumaOracle::getV3SqrtPriceAvg` function calculates time-weighted average prices (TWAP) from Uniswap V3 pools but fails to properly handle negative tick differences, leading to incorrect price calculations when the tick delta is negative and not perfectly divisible by the interval.


### Root Cause

The root cause is missing logic to round down negative tick values when calculating the arithmetic mean tick in  `NumaOracle::getV3SqrtPriceAvg`. The current implementation simply divides the tick delta by the interval without considering the rounding direction for negative values.
[NumaOracle.sol#L240-L247](https://github.com/sherlock-audit/2024-12-numa-audit/blob/ae1d7781efb4cb2c3a40c642887ddadeecabb97d/Numa/contracts/NumaProtocol/NumaOracle.sol#L240-L247)
```js
function getV3SqrtPriceAvg(
        address _uniswapV3Pool,
        uint32 _interval
    ) public view returns (uint160) {
        require(_interval > 0, "interval cannot be zero");
        //Returns TWAP prices for short and long intervals
        uint32[] memory secondsAgo = new uint32[](2);
        secondsAgo[0] = _interval; // from (before)
        secondsAgo[1] = 0; // to (now)

        (int56[] memory tickCumulatives, ) = IUniswapV3Pool(_uniswapV3Pool)
            .observe(secondsAgo);

        // tick(imprecise as it's an integer) to sqrtPriceX96
        return
            TickMath.getSqrtRatioAtTick(
                int24(
                    (tickCumulatives[1] - tickCumulatives[0]) /
                        int56(int32(_interval))
                )
            );
    }
```
The problem is that in case if `(tickCumulatives[1] - tickCumulatives[0])` is negative, the tick should be rounded down as it's done in the [OracleLibrary](https://github.com/Uniswap/v3-periphery/blob/697c2474757ea89fec12a4e6db16a574fe259610/contracts/libraries/OracleLibrary.sol#L16-L41) from uniswap.
```js
        int56 tickCumulativesDelta = tickCumulatives[1] - tickCumulatives[0];
        uint160 secondsPerLiquidityCumulativesDelta =
            secondsPerLiquidityCumulativeX128s[1] - secondsPerLiquidityCumulativeX128s[0];

        arithmeticMeanTick = int24(tickCumulativesDelta / secondsAgo);
        // Always round to negative infinity
@>      if (tickCumulativesDelta < 0 && (tickCumulativesDelta % secondsAgo != 0)) arithmeticMeanTick--;
```
As result, in case if `(tickCumulatives[1] - tickCumulatives[0])`is negative and `(tickCumulatives[1] - tickCumulatives[0]) % secondsAgo != 0`, then returned tick will be bigger then it should be, hence incorrect prices would be used.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The TWAP derived from the function will be inaccurate when the cumulative tick difference is negative and not evenly divisible by the interval. In such cases, the tick is effectively rounded up instead of down, causing the returned price to be higher than the actual value.

### PoC

_No response_

### Mitigation

```js
function getV3SqrtPriceAvg(address _uniswapV3Pool, uint32 _interval) public view returns (uint160) {
    require(_interval > 0, "interval cannot be zero");
    uint32[] memory secondsAgo = new uint32[](2);
    secondsAgo[0] = _interval;
    secondsAgo[1] = 0;

    (int56[] memory tickCumulatives, ) = IUniswapV3Pool(_uniswapV3Pool).observe(secondsAgo);
    
    int56 tickCumulativesDelta = tickCumulatives[1] - tickCumulatives[0];
    int24 arithmeticMeanTick = int24(tickCumulativesDelta / int56(int32(_interval)));
    
    // Add proper rounding for negative ticks
    if (tickCumulativesDelta < 0 && (tickCumulativesDelta % int56(int32(_interval)) != 0)) {
        arithmeticMeanTick--;
    }
    
    return TickMath.getSqrtRatioAtTick(arithmeticMeanTick);
}
```