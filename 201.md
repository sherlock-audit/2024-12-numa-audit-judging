Crazy Yellow Swift

Medium

# `VaultManager::updateBuyFeePID()` assumes different `ETH` bought than the actual, leading to incorrect `buyFeePID` adjustments.

### Summary

`VaultManager::updateBuyFeePID()`, which is supposed to adjust `buyFeePID`,  is calculating incorrectly the `ethAmount` involved since it calculates it after the changes have happened in the balance of vault.

### Root Cause

`VaultManager::updateBuyFeePID()` is taking the `numaAmount` that got involved in this `NUMA` interaction and depending on the `ETH` value of it and the difference of vault price and the `TWAP` Uniswap V3 Pool price, is increasing or decreasing the `buyFeePID` meaning adjusting the actual buy fee. Let's firstly see the `NumaVault::buy` to understand the whole flow :

```solidity
    function buy(
        uint _inputAmount,
        uint _minNumaAmount,
        address _receiver
    ) external whenNotPaused returns (uint _numaOut) {
        // CF will change so we need to update interest rates
        // Note that we call that function from vault and not vaultManager, because in multi vault case, we don't need to accrue interest on
        // other vaults as we use a "local CF"

        // rEth balance will change so we need to update debasing factors
        (
            ,
            uint criticalScaleForNumaPriceAndSellFee,

        ) = updateVaultAndUpdateDebasing();

        uint256 vaultsBalance = getVaultBalance();
        uint256 MAX = (max_percent * vaultsBalance) / BASE_1000;

        require(_inputAmount <= MAX, "must trade under max");

        _numaOut = buyNoMax(
            _inputAmount,
            _minNumaAmount,
            _receiver,
            criticalScaleForNumaPriceAndSellFee,
            true
        );
    }

    function buyNoMax(
        uint _inputAmount,
        uint _minNumaAmount,
        address _receiver,
        uint _criticalScaleForNumaPriceAndSellFee,
        bool _transferREth
    ) internal nonReentrant whenNotPaused returns (uint _numaOut) {
        // ...
@>        vaultManager.updateBuyFeePID(numaAmount, true);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-12-numa-audit/blob/ae1d7781efb4cb2c3a40c642887ddadeecabb97d/Numa/contracts/NumaProtocol/NumaVault.sol#L424C1-L453C1)

User is specifying the `rETH` amount that he wants to buy and, eventually, he gets minted the equivalent `NUMA` amount (with fee deducted) and he sends the `rETH` to the vault. At the end of it, as it is highlighted, a call on `VaultManager:: updateBuyFeePID()`, in order to update the `buyFeePID` according to the size of the interaction.

Let's see how the `VaultManager:: updateBuyFeePID()` will handle it :

```solidity
    function updateBuyFeePID(uint _numaAmount, bool _isVaultBuy) external {
        if (_numaAmount == 0) {
            return;
        }

        // ...

        if (address(printer) == address(0x0)) {
            buy_fee_PID = 0;
        } else {
            // ...
@>            uint _vaultBuyPrice = numaToEth(1 ether, PriceType.BuyPrice);

            // we use amount in Eth
@>            uint ethAmount = (_numaAmount * _vaultBuyPrice) / (1 ether);

            uint pctFromBuyPrice;
            if (_priceTWAP < _vaultBuyPrice) {
                //percentage down from buyPrice  in base 1000
                pctFromBuyPrice = 1000 - (1000 * _priceTWAP) / _vaultBuyPrice;
            }

            if ((pctFromBuyPrice < buyPID_incTriggerPct) && _isVaultBuy) {
                //_price is within incTriggerPct% of buyPrice, and is a vault buy
                uint buyPID_adj = (ethAmount * buyPID_incAmt) / (1 ether);
                buy_fee_PID = buy_fee_PID + buyPID_adj; //increment buyPID

                if (buy_fee_PID > buyPIDXhrAgo) {
                    if (((buy_fee_PID - buyPIDXhrAgo) > buyPID_incMaxRate)) {
                        //does change exceed max rate over Xhrs?
@>                        buy_fee_PID = buyPIDXhrAgo + buyPID_incMaxRate; //cap to max rate over 4hrs
                    }
                }

                if (buy_fee < (buyFee_max + buy_fee_PID)) {
                    //buyFee above maximum allowable = clip
                    buy_fee_PID = buy_fee - buyFee_max;
                }
            } else if (
                (pctFromBuyPrice > buyPID_decTriggerPct) && (!_isVaultBuy)
            ) {
                //LP15minTWAP is below decTriggerPct% from buyPrice.

                // if pctFromBuyPrice is more than 2 x buyfee, we use our decrease multiplier
                uint basefee = 1 ether - buy_fee;
                uint buyPID_multTriggerPct = (2 * basefee * 1000) / 1 ether;
@>                uint buyPID_adj = (ethAmount * buyPID_decAmt) / (1 ether);

                if (pctFromBuyPrice > buyPID_multTriggerPct) {
                    // do bigger reduction
                    buyPID_adj = buyPID_adj * buyPID_decMultiplier;
                }
                if (buyPID_adj < buy_fee_PID) {
                    buy_fee_PID -= buyPID_adj;
                } else {
                    buy_fee_PID = 0;
                }
            }

            // ...
        }
    }
```
[Link to code](https://github.com/sherlock-audit/2024-12-numa-audit/blob/ae1d7781efb4cb2c3a40c642887ddadeecabb97d/Numa/contracts/NumaProtocol/VaultManager.sol#L298C1-L378C6)

As it can be seen, it converts the `numaAmount` to `ethAmount` in order to retrieve the size of the interaction. However, this `ETH` amount is not the actual `ETH` amount that got bought, sold or converted to `nuAsset` since it is recalculated after the interaction occured. **When the bought happened the user deposited X `ETH` value which is not equal to the recalculated Y `ETH` value in `updateBuyFeePID`. The whole problem occurs because it uses the `numaToEth()` to calculate the `_vaultBuyPrice` but this is **new** `buyPrice`, not the one that the user actually bought `NUMA`.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Impact : **Medium**
Likelihood **High**

The severity of this vulnerability is High since incorrect adjustments on `buyFeePID` will be executed with the calculated `ETH` value to not be the real one. In the case of a `NUMA` buy, the actual ETH deposited will be `X` but the `updateBuyFeePID` will calculated as `Y` ( which will be bigger than `X` since, with the new numbers, the `NUMA` price increased). This will increase/decrease, for example, the `buyFeePID` more than what it should. In the case of the `POC` for example, the incorrect adjustment will be as high as `5.17%`.

### PoC

To reproduce this bug, add the following test in the `contracts/Test/Vault.t.sol` :
```solidity
    function test_difference_in_actual_eth_bought_and_update_fee_eth_bought() public {
        uint inputreth = 15 ether;
        uint256 numaAmount = vault.lstToNuma(inputreth);

        vm.startPrank(userA);
        rEth.approve(address(vault), inputreth);
        uint buyAmount = vault.buy(inputreth, numaAmount, userA);
    }
```
After this, just add two loggers inside these functions and run `yarn install
FOUNDRY_PROFILE=lite forge test --mt test_difference_in_actual_eth_bought_and_update_fee_eth_bought -vv` :

```diff
    function buyNoMax(
         // ...
    ) internal nonReentrant whenNotPaused returns (uint _numaOut) {
        // SAME CODE AS buy() but no max amount (used for liquidations)
        // buys can be paused if we want to force people to buy from other vaults
        require(!buyPaused, "buy paused");
        require(_inputAmount > MIN, "must trade over min");
+       console.log("Actual deposited ETH in the `buy` :                  ", _inputAmount * last_lsttokenvalueWei / 1e18);

        // ...
}

    function updateBuyFeePID(uint _numaAmount, bool _isVaultBuy) external {
        if (_numaAmount == 0) {
            return;
        }

        uint currentBlockts = block.timestamp;
        if (nextCheckBlock == 0) {
            nextCheckBlock = currentBlockts + nextCheckBlockWindowDelta;
        }
        // when delta time is reached or PID is below last reference we reset reference
        else if (currentBlockts > nextCheckBlock) {
            //reset the increment max rate params
            buyPIDXhrAgo = buy_fee_PID;
            //set new block height +xhrs from now
            nextCheckBlock = currentBlockts + nextCheckBlockWindowDelta;
        }

        if (address(printer) == address(0x0)) {
            buy_fee_PID = 0;
        } else {
            require(
                isVault(msg.sender) || (msg.sender == address(printer)),
                "only vault&printer"
            );
            uint _priceTWAP = printer.getTWAPPriceInEth(1 ether, twapPID);
            uint _vaultBuyPrice = numaToEth(1 ether, PriceType.BuyPrice);

            // we use amount in Eth
            uint ethAmount = (_numaAmount * _vaultBuyPrice) / (1 ether);
+           console.log("Supposed ETH deposited during `updateBuyFeePID` :    ", ethAmount);
            // ...
        }
}
```

The console result will be this :
```sh
  Actual deposited ETH in the `buy` :                   16871344727024350695
  Supposed ETH deposited during `updateBuyFeePID` :     17767364320151451447
```

This essentially mean that user deposited actually 16871344727024350695 `ETH` but the system updated the `buyfeePID` as if the bought was `17767364320151451447` ETH. The difference of them is `5.17352%` and that means that the `buyFeePID` will be increased/decreased `5.17352%` more than what it must.

### Mitigation

Consider passing the `ETH` value during the call to `VaultManager.updateBuyFeePID` in order to have the correct size that got bought/sold.