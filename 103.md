Agreeable Coral Lion

Medium

# OracleUtils.sol will report invalid rETH/ETH price causing funds loss for protocol or user due to relying on Chainlink feed min/maxAnswer checks only

### Summary

ChainLink [rETH/ETH](https://data.chain.link/feeds/arbitrum/mainnet/reth-eth) price feed [aggregator](https://arbiscan.io/address/0x1B3224c0847242f6d0FbC31087117459596FfcCF) returns `1` for `minAnswer()` and a huge value for `maxAnswer()` which will cause `OracleUtils.sol` to report an invalid amount of ETH/Token causing loss of funds for protocol or user if for some reason the ChainLink rETH/ETH price feed malfunctions and reports unrealistic values and `OracleUtils.sol` will continue to rely on min/maxAnswer checks only.

### Root Cause

The choice to rely only on `minAnswer()`/`maxAnswer()` checks in `OracleUtils.sol` in functions `ethToToken()`, `ethToTokenRoundUp()`, `tokenToEth()` and `tokenToEthRoundUp()` is a mistake as in case of ChainLink [rETH/ETH](https://data.chain.link/feeds/arbitrum/mainnet/reth-eth) price feed malfunction, the total amount of ETH/Token returned by `OracleUtils` may be unrealistic and differing significantly from current market price.

Currently returned minAnswer: 1
Currently returned maxAnswer: 95780971304118053647396689196894323976171195136475135

Link to one of the relevant checks: https://github.com/sherlock-audit/2024-12-numa-audit/blob/main/Numa/contracts/libraries/OracleUtils.sol#L70C1-L78C11

### Internal pre-conditions

N/A

### External pre-conditions

ChainLink [rETH/ETH](https://data.chain.link/feeds/arbitrum/mainnet/reth-eth) price feed malfunctions and reports unrealistically low/high price or one that is significantly different from current market price.

### Attack Path

1. An attacker notices the [rETH/ETH](https://data.chain.link/feeds/arbitrum/mainnet/reth-eth) price feed malfunctions and reports invalid values that favour the user
2. An attacker then uses any protocol functions that rely on this price and causes loss of funds for the protocol

### Impact

In case of an attack path - impact as in the `Attack Path` paragraph.

If the [rETH/ETH](https://data.chain.link/feeds/arbitrum/mainnet/reth-eth) price feed malfunctions and reports invalid values that favour the protocol a user can be using the protocol as intended but due to invalid price the user loses funds in favour of the protocol.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.20;

import {Test} from "forge-std/Test.sol";
import {OracleUtils} from "../libraries/OracleUtils.sol";
import {MockV3Aggregator} from "@chainlink/contracts/src/v0.8/tests/MockV3Aggregator.sol";
import {IChainlinkAggregator} from "../interfaces/IChainlinkAggregator.sol";
import {AggregatorV2V3Interface} from "@chainlink/contracts/src/v0.8/interfaces/AggregatorV2V3Interface.sol";
import {IChainlinkAggregator} from "../interfaces/IChainlinkAggregator.sol";

contract AuditOracleUtilsTest is Test {
    function testRETHMinMaxChainlinkAnswers() public {
        vm.createSelectFork("https://arb1.arbitrum.io/rpc");
        IChainlinkAggregator arbitrumREthFeed = IChainlinkAggregator(0x1B3224c0847242f6d0FbC31087117459596FfcCF);

        assertEq(arbitrumREthFeed.minAnswer(), 1);
        assertEq(arbitrumREthFeed.maxAnswer(), 95780971304118053647396689196894323976171195136475135);
    }

    function testRETHMinMaxRisk() public {
        MockV3Aggregator sequencerUptimeFeed = new MockV3Aggregator(18, 0);
        ChainlinkAggregator rEthAggregator = new ChainlinkAggregator();
        MockV3AggregatorWithAggregatorData rEthPriceFeed = new MockV3AggregatorWithAggregatorData(18, 2 ether, address(rEthAggregator));
        OracleUtils oracleUtils = new OracleUtils(address(sequencerUptimeFeed));

        vm.warp(10000); // Warp so that we are out of the grace period

        // "Normal" ETH/rETH price - 2e18
        rEthPriceFeed.updateRoundData(1, 2 ether, 9005, 9005);
        uint256 tokenAmount = oracleUtils.ethToToken(1 ether, address(rEthPriceFeed), 1000, 18);
        assertEq(tokenAmount, 0.5 ether);

        // Very low ETH/rETH price - 2
        rEthPriceFeed.updateRoundData(2, 2, 9005, 9005);
        tokenAmount = oracleUtils.ethToToken(1 ether, address(rEthPriceFeed), 1000, 18);
        assertEq(tokenAmount, 5e17 ether);

        // Very high ETH/rETH price - 1e35
        rEthPriceFeed.updateRoundData(3, 1e35, 9005, 9005);
        tokenAmount = oracleUtils.ethToToken(1 ether, address(rEthPriceFeed), 1000, 18);
        assertEq(tokenAmount, 10);
    }
}

contract ChainlinkAggregator is IChainlinkAggregator {
    function minAnswer() external pure returns (int192) {
        return 1;
    }

    function maxAnswer() external pure returns (int192) {
        return 95780971304118053647396689196894323976171195136475135;
    }
}

contract MockV3AggregatorWithAggregatorData is MockV3Aggregator {
    address aggregatorAddress;

    constructor(uint8 _decimals, int256 _initialAnswer, address _aggregator) MockV3Aggregator(_decimals, _initialAnswer) {
        aggregatorAddress = _aggregator;
    }

    function aggregator() external view returns (address) {
        return aggregatorAddress;
    }
}

```

### Mitigation

Consider adding additional checks if the reported value is in at least reasonable bounds.