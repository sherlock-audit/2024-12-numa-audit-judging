Crazy Yellow Swift

Medium

# `rewardsFromDebt` are accumulating even if there is no `rwd_address` causing unexpected results during repayments.

### Summary

The `rewardsFromDebt` during `NumaVault::extractInternal` are accumulating even if there is no `rwd_address`.

### Root Cause

In `NumaVault` the reward extraction is happening in order to handle the appreciation of `rETH/ETH` and capture the extra value of the rETH that are in the vault. Protocol can  either set a `rwd_address` and get this value every 24 hours or do not set a `rwd_address` and let the extra value to stay in the vault and strengthen the collateral. Since the vault is offering loans of rETH, the process of handling the rewards for in vault rETH and for borrowed rETH is a bit different (since the debt rETH are not technically in the vault) but the logic must remain the same.
- For rETH in vault, every 24 hours the extra value of rETH (difference in `rETH/ETH` price) is captured and transferred out to the `rwd_address`, **if there is one**. If there is not, this means that it will stay in the `NumaVault` in order to increase the collateralization further and these specific rewards will **never** be send to the `rwd_address`. Let's see the `NumaVault::extractInternal()` :

```solidity
    function extractInternal(
        uint rwd,
        uint currentvalueWei,
        uint rwdDebt
    ) internal {
        last_extracttimestamp = block.timestamp;
        last_lsttokenvalueWei = currentvalueWei;

        // rewards from debt are not sent, they are accumulated to be sent when there's a repay
        rewardsFromDebt += rwdDebt;
@>        if (rwd_address != address(0)) {
            SafeERC20.safeTransfer(IERC20(lstToken), rwd_address, rwd);
            if (isContract(rwd_address) && isRwdReceiver) {
                // we don't check result as contract might not implement the deposit function (if multi sig for example)
                rwd_address.call(
                    abi.encodeWithSignature("DepositFromVault(uint256)", rwd)
                );
            }
        }
        emit RewardsExtracted(rwd, currentvalueWei);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-12-numa-audit/blob/ae1d7781efb4cb2c3a40c642887ddadeecabb97d/Numa/contracts/NumaProtocol/NumaVault.sol#L349C1-L369C6)
- In the case that the rETH are borrowed (meaning they represent the `debt`), the process is the same with the only difference that the rewards are "captured "/saved in order to be sent to `rwd_address` when the rETH are being repaid. However, as we can see in the `extractInternal` the `rewadrsFromDebt` are increased even if there is not a `rwd_address`. This means that these rewards are **not* expected to be sent to the reward address, but instead they , incorrectly, being accumulated to the `rewardsFromDebt`. This will be a problem afterwards during repayment since these rewards were not intended to be sent to the reward address but  if the `rwd_address` has been set, these rewards that were accumulate previously will be actually sent to him. Let's see the `NumaVault::repay` :
```solidity
    function repay(uint _amount) external {
        require(msg.sender == address(cLstToken));
        require(_amount > 0, "amount <= 0");
        require(_amount <= debt, "repay more than debt");

        updateVaultAndUpdateDebasing();

        // repay
        SafeERC20.safeTransferFrom(
            lstToken,
            msg.sender,
            address(this),
            _amount
        );
        // we will use some repaid amount as rewards from our accumulated virtual rewards from debt
@>        uint extractedRwdFromDebt = FullMath.mulDiv(
            rewardsFromDebt,
            _amount,
            debt
        );

@>        if ((extractedRwdFromDebt > 0) && (rwd_address != address(0))) {
            rewardsFromDebt -= extractedRwdFromDebt;
            SafeERC20.safeTransfer(
                IERC20(lstToken),
                rwd_address,
                extractedRwdFromDebt
            );
            if (isContract(rwd_address) && isRwdReceiver) {
                // we don't check result as contract might not implement the deposit function (if multi sig for example)
                rwd_address.call(
                    abi.encodeWithSignature(
                        "DepositFromVault(uint256)",
                        extractedRwdFromDebt
                    )
                );
            }
            emit RewardsDebtExtracted(extractedRwdFromDebt);
        }

        debt = debt - _amount;
        emit RepaidVault(_amount);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-12-numa-audit/blob/ae1d7781efb4cb2c3a40c642887ddadeecabb97d/Numa/contracts/NumaProtocol/NumaVault.sol#L777C1-L819C6)

Not only that, but for the whole meantime these rewards will be considerred in the rewards calculation in `rewardsValue()`.



### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. `rwd_address` is not set meaning that all rewards are intended to be accumulated **in** the vault and stay inside in order to strengthen the numa collateralization.
2. rewards from rETH inside the vault**, correctly,** are not being transferred out. rewards from debt**, incorrectly,** are captured in the `rewardsFromDebt`
3. After some time, `rwd_address_ is being set meaning that the protocol wants **from now on** to get the rewards of the appreciation of `rETH/ETH`.
4. A repayment is made and the **previously** captured rewards are send to the new `rwd_address` while in the time of their accumulation they were not supposed to be extracted.

### Impact

**Medium**. Incorrect reward extraction for `rewardsFromDebt`. These will rewards were not supposed to be extracted but instead they will affect the calculations in `rewardsValue`, they will be accumulated to the `rewardsFromDebt` and later if a `rwd_address` has been set, they will be extracted.

### PoC

_No response_

### Mitigation

Consider making this code change and extract rewards correctly if there is a `rwd_address` :
```diff
    function extractInternal(
        uint rwd,
        uint currentvalueWei,
        uint rwdDebt
    ) internal {
        last_extracttimestamp = block.timestamp;
        last_lsttokenvalueWei = currentvalueWei;

-       // rewards from debt are not sent, they are accumulated to be sent when there's a repay
-        rewardsFromDebt += rwdDebt;
        if (rwd_address != address(0)) {
+          rewardsFromDebt += rwdDebt;
            SafeERC20.safeTransfer(IERC20(lstToken), rwd_address, rwd);
            if (isContract(rwd_address) && isRwdReceiver) {
                // we don't check result as contract might not implement the deposit function (if multi sig for example)
                rwd_address.call(
                    abi.encodeWithSignature("DepositFromVault(uint256)", rwd)
                );
            }
        }
        emit RewardsExtracted(rwd, currentvalueWei);
    }
```