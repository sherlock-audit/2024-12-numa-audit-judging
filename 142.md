Kind Brick Millipede

Medium

# Whenever the liquidation's payer is `address(this)`, repaying the debt with `type(uint256).max` will always fail with an underflow error

### Summary

Whenever the `payer` of the debt is the `address(this)` (i.e. the `CToken`), the repayment will always fail if the specified amount to repay is capped at `type(uint256).max`, which may be the case when the caller wants to repay 100% of the current debt.

### Root Cause

- https://github.com/sherlock-audit/2024-12-numa-audit/blob/ae1d7781efb4cb2c3a40c642887ddadeecabb97d/Numa/contracts/lending/CToken.sol#L844-L879

```solidity
    /**
     * @notice Borrows are repaid by another user (possibly the borrower).
     * @param payer the account paying off the borrow
     * @param borrower the account with the debt being payed off
     * @param repayAmount the amount of underlying tokens being returned, or -1 for the full outstanding amount
     * @return (uint) the actual repayment amount.
     */
    function repayBorrowFresh(
        address payer,
        address borrower,
        uint repayAmount
    ) internal virtual returns (uint) {
        /* Fail if repayBorrow not allowed */
        uint allowed = comptroller.repayBorrowAllowed(
            address(this),
            payer,
            borrower,
            repayAmount
        );
        if (allowed != 0) {
            revert RepayBorrowComptrollerRejection(allowed);
        }

        /* Verify market's block number equals current block number */
        if (accrualBlockNumber != getBlockNumber()) {
            revert RepayBorrowFreshnessCheck();
        }

        /* We fetch the amount the borrower owes, with accumulated interest */
        uint accountBorrowsPrev = borrowBalanceStoredInternal(borrower);

        /* If repayAmount == -1, repayAmount = accountBorrows */
        uint repayAmountFinal = repayAmount == type(uint).max
            ? accountBorrowsPrev
            : repayAmount;

        /////////////////////////
        // EFFECTS & INTERACTIONS
        // (No safe failures beyond this point)

        /*
         * We call doTransferIn for the payer and the repayAmount
         *  Note: The cToken must handle variations between ERC-20 and ETH underlying.
         *  On success, the cToken holds an additional repayAmount of cash.
         *  doTransferIn reverts if anything goes wrong, since we can't be sure if side effects occurred.
         *   it returns the amount actually transferred, in case of a fee.
         */
        // Numa modif, we can use this function to repay leverage and payer is CToken (amount comes from flashloan) @audit-info check out this case carefully!
        // so we need to handle that case
        uint actualRepayAmount = repayAmount;
        if (payer != address(this)) {
            // transfer needed
            actualRepayAmount = doTransferIn(payer, repayAmountFinal); // @audit-info: check this here too!!!
        }

        /*
         * We calculate the new borrower and total borrow balances, failing on underflow:
         *  accountBorrowsNew = accountBorrows - actualRepayAmount
         *  totalBorrowsNew = totalBorrows - actualRepayAmount
         */
        uint accountBorrowsNew = accountBorrowsPrev - actualRepayAmount; // this will always fail for repayAmount being == -1 type(uint256.max)
        uint totalBorrowsNew = totalBorrows - actualRepayAmount;

        /* We write the previously calculated values into storage */
        accountBorrows[borrower].principal = accountBorrowsNew;
        accountBorrows[borrower].interestIndex = borrowIndex;
        totalBorrows = totalBorrowsNew;

        /* We emit a RepayBorrow event */
        emit RepayBorrow(
            payer,
            borrower,
            actualRepayAmount,
            accountBorrowsNew,
            totalBorrowsNew
        );

        return actualRepayAmount;
    }

```

Due to not accounting for changes in the `actualRepayAmount` which may be needed whenever there's a case for both the conditions of the `payer == address(this)` and `repayAmount == type(uint256).max` colliding, there'll be a revert due to an underflow in the following lines of code:

```solidity
        uint accountBorrowsNew = accountBorrowsPrev - actualRepayAmount; // this will always fail for repayAmount being == -1 type(uint256.max)
        uint totalBorrowsNew = totalBorrows - actualRepayAmount;
```

In reality, in such a case, the `repayAmountFinal` should be subtracted:
```solidity
        /* If repayAmount == -1, repayAmount = accountBorrows */
        uint repayAmountFinal = repayAmount == type(uint).max
            ? accountBorrowsPrev
            : repayAmount;
```

### Internal pre-conditions

_No response_

### External pre-conditions

The borrower repays the debt with the flashloaned funds, (which is usually the case when `payer == address(this)`, if I'm not mistaken!), and specifies `repayAmount = type(uint256).max` to repay the full balance of the debt.

### Attack Path

_No response_

### Impact

There's no way to make the contract adjust the amount to repay to be capped at the current debt balance, which is a especially critical feature, considering that there's a **known** attack of dust repayments front-running occurences, which means that an attacker can DoS repayments for a specific borrower by front-running their transactions with `1 wei` repay amounts, and the borrower can never repay the debt in full then.

The `repayAmount = type(uint256).max` option should be provided in order for the payers to be able to pay the whole debt in full, so that the contract simply caps the amount to repay based on whatever the borrower's debt currently is.

### PoC

_No response_

### Mitigation

```diff
    /**
     * @notice Borrows are repaid by another user (possibly the borrower).
     * @param payer the account paying off the borrow
     * @param borrower the account with the debt being payed off
     * @param repayAmount the amount of underlying tokens being returned, or -1 for the full outstanding amount
     * @return (uint) the actual repayment amount.
     */
    function repayBorrowFresh(
        address payer,
        address borrower,
        uint repayAmount
    ) internal virtual returns (uint) {
        /* Fail if repayBorrow not allowed */
        uint allowed = comptroller.repayBorrowAllowed(
            address(this),
            payer,
            borrower,
            repayAmount
        );
        if (allowed != 0) {
            revert RepayBorrowComptrollerRejection(allowed);
        }

        /* Verify market's block number equals current block number */
        if (accrualBlockNumber != getBlockNumber()) {
            revert RepayBorrowFreshnessCheck();
        }

        /* We fetch the amount the borrower owes, with accumulated interest */
        uint accountBorrowsPrev = borrowBalanceStoredInternal(borrower);

        /* If repayAmount == -1, repayAmount = accountBorrows */
        uint repayAmountFinal = repayAmount == type(uint).max
            ? accountBorrowsPrev
            : repayAmount;

        /////////////////////////
        // EFFECTS & INTERACTIONS
        // (No safe failures beyond this point)

        /*
         * We call doTransferIn for the payer and the repayAmount
         *  Note: The cToken must handle variations between ERC-20 and ETH underlying.
         *  On success, the cToken holds an additional repayAmount of cash.
         *  doTransferIn reverts if anything goes wrong, since we can't be sure if side effects occurred.
         *   it returns the amount actually transferred, in case of a fee.
         */
        // Numa modif, we can use this function to repay leverage and payer is CToken (amount comes from flashloan) @audit-info check out this case carefully!
        // so we need to handle that case
        uint actualRepayAmount = repayAmount;
        if (payer != address(this)) {
            // transfer needed
            actualRepayAmount = doTransferIn(payer, repayAmountFinal); // @audit-info: check this here too!!!
        }
+       else if (repayAmount == type(uint256).max) {
+         repayAmount = accountBorrowsPrev;
+       }

        /*
         * We calculate the new borrower and total borrow balances, failing on underflow:
         *  accountBorrowsNew = accountBorrows - actualRepayAmount
         *  totalBorrowsNew = totalBorrows - actualRepayAmount
         */
        uint accountBorrowsNew = accountBorrowsPrev - actualRepayAmount; // this will always fail for repayAmount being == -1 type(uint256.max)
        uint totalBorrowsNew = totalBorrows - actualRepayAmount;

        /* We write the previously calculated values into storage */
        accountBorrows[borrower].principal = accountBorrowsNew;
        accountBorrows[borrower].interestIndex = borrowIndex;
        totalBorrows = totalBorrowsNew;

        /* We emit a RepayBorrow event */
        emit RepayBorrow(
            payer,
            borrower,
            actualRepayAmount,
            accountBorrowsNew,
            totalBorrowsNew
        );

        return actualRepayAmount;
    }
```